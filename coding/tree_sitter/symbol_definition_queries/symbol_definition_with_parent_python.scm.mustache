
(class_definition
    name: (identifier) @parentName
    body: (_
        [
            ;we could try using a mustache partial here pointing to symbol_definition_python instead of copying it
            (
                (type_alias_statement
                    (type (identifier) @childName)
                )
                (#eq? @childName "{{childSymbolName}}")
            ) @definition

            (expression_statement
                (assignment
                    left: (identifier) @childName
                )
                (#eq? @childName "{{childSymbolName}}")
            ) @definition

            (
                ((decorator)+ .)?
                (class_definition
                    name: (identifier) @childName
                )
                (#eq? @childName "{{childSymbolName}}")
            ) @definition

            (
                ((decorator)+ .)?
                (function_definition
                    name: (identifier) @childName
                )
                (#eq? @childName "{{childSymbolName}}")
            ) @definition
        ]
    )
    (#eq? @parentName "{{parentSymbolName}}")
)
